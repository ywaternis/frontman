# Frontman Server
# Copyright (C) 2025 Frontman AI
#
# Licensed under the AGPL-3.0 — see LICENSE for details.
# Additional terms apply — see AI-SUPPLEMENTARY-TERMS.md

defmodule FrontmanServer.Tasks.Execution.LLMClient do
  @moduledoc """
  SwarmAi.LLM implementation using ReqLLM.

  Stream-first design: returns a lazy stream of chunks that can be
  consumed with callbacks or collected into a Response.

  API key resolution happens at the domain layer (Tasks context) before
  this client is created. The resolved key is passed via `llm_opts[:api_key]`.
  """

  use TypedStruct

  alias FrontmanServer.Providers
  alias SwarmAi.SchemaTransformer

  typedstruct do
    field(:model, String.t(), default: Providers.default_model())
    field(:tools, [SwarmAi.Tool.t()], default: [])
    # llm_opts must include :api_key (resolved at domain layer)
    field(:llm_opts, keyword(), default: [])
  end

  @doc """
  Creates a new LLMClient.

  ## Options

  - `:model` - Model spec string (default: "openrouter:google/gemini-3-flash-preview")
  - `:tools` - List of SwarmAi.Tool structs
  - `:llm_opts` - Options for ReqLLM, must include `:api_key`
  """
  def new(opts \\ []) do
    struct!(__MODULE__, opts)
  end

  @doc """
  Converts SwarmAi.Tool to ReqLLM.Tool format.
  Normalizes schemas for OpenAI-compatible providers that require strict mode.
  """
  @spec to_reqllm_tool(SwarmAi.Tool.t(), String.t(), keyword()) :: ReqLLM.Tool.t()
  def to_reqllm_tool(%SwarmAi.Tool{} = tool, model, _opts \\ []) do
    provider = SchemaTransformer.provider_for_model(model)
    schema = SchemaTransformer.transform(tool.parameter_schema, provider)
    strict? = provider == :openai_strict

    ReqLLM.Tool.new!(
      name: tool.name,
      description: tool.description,
      parameter_schema: schema,
      strict: strict?,
      callback: fn _args -> {:ok, nil} end
    )
  end
end

defimpl SwarmAi.LLM, for: FrontmanServer.Tasks.Execution.LLMClient do
  alias FrontmanServer.Tasks.Execution.LLMClient
  alias FrontmanServer.Tasks.Execution.LLMError
  alias FrontmanServer.Tasks.Execution.LLMProvider
  alias FrontmanServer.Tasks.{MessageOptimizer, StreamCleanup, StreamStallTimeout}
  alias SwarmAi.Message
  alias SwarmAi.Message.ContentPart
  alias SwarmAi.SchemaTransformer

  require Logger

  def stream(client, messages, _opts) do
    reqllm_tools =
      Enum.map(client.tools, &LLMClient.to_reqllm_tool(&1, client.model, client.llm_opts))

    # API key must be provided via llm_opts (resolved at domain layer)
    llm_opts =
      client.llm_opts
      |> Keyword.put_new(:tools, reqllm_tools)
      |> Keyword.reject(fn
        {:parallel_tool_calls, _value} -> true
        {_key, value} -> value == []
      end)

    # Run MessageOptimizer here (not just at task startup) so that tool results
    # accumulated inside the swarm loop are also truncated. Without this, long
    # tool-calling chains accumulate dozens of full-size tool results and the
    # request body grows until Anthropic closes the connection.
    reqllm_messages =
      messages
      |> Enum.map(&to_reqllm_message/1)
      |> MessageOptimizer.optimize()
      |> strip_images_unless_supported(client.model)

    case LLMProvider.stream_text(client.model, reqllm_messages, llm_opts) do
      {:ok, response} ->
        stall_timeout_ms =
          Application.fetch_env!(:frontman_server, :stream_stall_timeout_ms)

        reqllm_stream =
          response.stream
          |> StreamStallTimeout.wrap_stream(stall_timeout_ms: stall_timeout_ms)
          |> Stream.map(&normalize_reqllm_chunk/1)
          |> StreamCleanup.wrap_stream(response.cancel)

        {:ok, reqllm_stream}

      {:error, reason} ->
        Logger.error("LLMClient.stream ReqLLM.stream_text failed: #{inspect(reason)}")
        {:error, reason}
    end
  end

  defp normalize_reqllm_chunk(%{type: :content} = chunk) do
    chunk
  end

  defp normalize_reqllm_chunk(%{type: :thinking} = chunk) do
    chunk
  end

  defp normalize_reqllm_chunk(
         %{type: :tool_call, arguments: arguments, metadata: metadata} = chunk
       ) do
    metadata = metadata || %{}

    id = metadata[:id] || metadata["id"] || "call_#{:erlang.unique_integer([:positive])}"

    index = normalize_index(metadata[:index] || metadata["index"])

    normalized_arguments =
      case arguments do
        nil -> %{}
        _ -> arguments
      end

    normalized_metadata =
      metadata
      |> Map.put(:id, id)
      |> Map.put(:index, index)

    %{
      chunk
      | arguments: normalized_arguments,
        metadata: normalized_metadata
    }
  end

  defp normalize_reqllm_chunk(%{type: :meta} = chunk) do
    chunk
  end

  # Legacy compatibility path for ReqLLM builds that emit :error chunks.
  # Current ReqLLM versions raise ReqLLM.Error.API.Stream instead; those are
  # classified in ExecutionEvent.classify_error/1.
  defp normalize_reqllm_chunk(%{type: :error, text: text, metadata: %{error: original}})
       when is_binary(text) do
    classify_llm_error(original, text)
  end

  defp normalize_reqllm_chunk(%{type: :error, text: text})
       when is_binary(text) do
    classify_llm_error(nil, text)
  end

  defp normalize_reqllm_chunk(%{type: :error} = chunk) do
    raise "LLM stream error: #{inspect(chunk, limit: :infinity)}"
  end

  defp normalize_reqllm_chunk(%{type: unknown_type} = chunk)
       when unknown_type not in [:content, :thinking, :tool_call, :meta, :error] do
    raise "Unknown chunk TYPE from ReqLLM: #{inspect(unknown_type)}. " <>
            "Full chunk: #{inspect(chunk, limit: :infinity)}"
  end

  defp normalize_reqllm_chunk(malformed_chunk) do
    raise "Malformed chunk from ReqLLM (missing or invalid type): #{inspect(malformed_chunk, limit: :infinity)}"
  end

  defp normalize_index(index) when is_integer(index), do: index

  defp normalize_index(index) when is_binary(index) do
    case Integer.parse(index) do
      {value, ""} -> value
      _other -> 0
    end
  end

  defp normalize_index(_index), do: 0

  # Classify LLM API errors by HTTP status and raise a typed LLMError.
  # The original error is a ReqLLM.Error.API.Request with :status and :reason.
  defp classify_llm_error(%{status: status}, _text) when status in [401, 403] do
    raise LLMError,
      message: "Authentication failed — your API key may be invalid or expired (HTTP #{status})",
      category: "auth",
      retryable: false
  end

  defp classify_llm_error(%{status: 400, reason: reason}, _text) when is_binary(reason) do
    raise LLMError,
      message: "Bad request — the provider rejected the request: #{reason}",
      category: "unknown",
      retryable: false
  end

  defp classify_llm_error(%{status: 400}, text) do
    raise LLMError,
      message: "Bad request — the provider rejected the request: #{text}",
      category: "unknown",
      retryable: false
  end

  defp classify_llm_error(%{status: 402}, _text) do
    raise LLMError,
      message:
        "Payment required — your account balance is insufficient or billing is not configured (HTTP 402)",
      category: "billing",
      retryable: false
  end

  defp classify_llm_error(%{status: 413}, _text) do
    raise LLMError,
      message:
        "Payload too large — the request exceeded the provider's size limit. Try reducing image size or message length (HTTP 413)",
      category: "payload_too_large",
      retryable: false
  end

  defp classify_llm_error(%{status: 429}, _text) do
    raise LLMError,
      message: "Rate limited — the provider is throttling requests. Please try again shortly.",
      category: "rate_limit",
      retryable: true
  end

  defp classify_llm_error(%{status: status}, _text) when status >= 500 do
    raise LLMError,
      message:
        "Provider error — the LLM service returned an internal error (HTTP #{status}). Please try again.",
      category: "overload",
      retryable: true
  end

  defp classify_llm_error(%{status: status, reason: reason}, _text)
       when is_integer(status) and is_binary(reason) do
    raise LLMError,
      message: "LLM error (HTTP #{status}): #{reason}",
      category: "unknown",
      retryable: false
  end

  defp classify_llm_error(_, text) do
    raise LLMError,
      message: "LLM stream error: #{text}",
      category: "unknown",
      retryable: false
  end

  # --- SwarmAi.Message -> ReqLLM.Message conversion ---

  defp to_reqllm_message(%Message.System{} = msg) do
    %ReqLLM.Message{role: :system, content: Enum.map(msg.content, &to_reqllm_content_part/1)}
  end

  defp to_reqllm_message(%Message.User{} = msg) do
    %ReqLLM.Message{role: :user, content: Enum.map(msg.content, &to_reqllm_content_part/1)}
  end

  defp to_reqllm_message(%Message.Assistant{} = msg) do
    %ReqLLM.Message{
      role: :assistant,
      content: Enum.map(msg.content, &to_reqllm_content_part/1),
      tool_calls: to_reqllm_tool_calls(msg.tool_calls),
      metadata: msg.metadata
    }
  end

  defp to_reqllm_message(%Message.Tool{} = msg) do
    %ReqLLM.Message{
      role: :tool,
      content: Enum.map(msg.content, &to_reqllm_content_part/1),
      tool_call_id: msg.tool_call_id,
      name: msg.name,
      metadata: msg.metadata
    }
  end

  defp to_reqllm_content_part(%ContentPart{type: :text, text: text}) do
    ReqLLM.Message.ContentPart.text(text)
  end

  defp to_reqllm_content_part(%ContentPart{type: :image, data: data, media_type: mt}) do
    ReqLLM.Message.ContentPart.image(data, mt)
  end

  defp to_reqllm_content_part(%ContentPart{type: :image_url, url: url}) do
    ReqLLM.Message.ContentPart.image_url(url)
  end

  defp strip_images_unless_supported(messages, model) do
    case ReqLLM.model(model) do
      {:ok, %{modalities: %{input: input}}} when is_list(input) ->
        if :image in input, do: messages, else: Enum.map(messages, &strip_message_images/1)

      _ ->
        messages
    end
  end

  defp strip_message_images(%ReqLLM.Message{content: content} = message) do
    %{message | content: Enum.map(content, &strip_image_part/1)}
  end

  defp strip_image_part(%ReqLLM.Message.ContentPart{type: type})
       when type in [:image, :image_url] do
    ReqLLM.Message.ContentPart.text(
      "[Image omitted: selected model does not support image input]"
    )
  end

  defp strip_image_part(part), do: part

  defp to_reqllm_tool_calls([]), do: nil
  defp to_reqllm_tool_calls(nil), do: nil

  defp to_reqllm_tool_calls(tool_calls) do
    Enum.map(tool_calls, fn tc ->
      arguments = strip_null_args(tc.arguments)
      ReqLLM.ToolCall.new(tc.id, tc.name, arguments)
    end)
  end

  # Strip null values from tool call arguments in conversation history.
  # OpenAI strict mode makes optional fields nullable, so the model sends null.
  # Clean these before sending back in the next turn.
  defp strip_null_args(arguments) when is_binary(arguments) do
    case Jason.decode(arguments) do
      {:ok, args} when is_map(args) ->
        Jason.encode!(SwarmAi.SchemaTransformer.strip_nulls(args))

      _ ->
        arguments
    end
  end

  defp strip_null_args(arguments), do: arguments
end
