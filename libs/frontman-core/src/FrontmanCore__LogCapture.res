@moduledoc(`
Captures console logs, build output, and uncaught errors in Node.js environments.

Generic version for any framework (Vite, Astro, etc.). Logs are stored in a
circular buffer and can be queried with filters. Call \`initialize()\` once at
app startup. Browser environments are automatically skipped.

Uses broader default patterns than Next.js-specific version to capture
Vite/Astro/general build tool output.
`)
module CircularBuffer = FrontmanCore__CircularBuffer

let isBrowser = (): bool => %raw(`typeof window !== 'undefined'`)

// Custom globalThis properties for Frontman
let getPatchedFlag = (): option<bool> => %raw(`globalThis.__FRONTMAN_CORE_CONSOLE_PATCHED__`)
let setPatchedFlag = (_value: bool): unit =>
  %raw(`globalThis.__FRONTMAN_CORE_CONSOLE_PATCHED__ = _value`)

@schema
type logLevel =
  | @as("console") Console
  | @as("build") Build
  | @as("error") Error

@schema
type consoleMethod =
  | @as("log") Log
  | @as("info") Info
  | @as("warn") Warn
  | @as("error") ConsoleError
  | @as("debug") Debug

@schema
type logEntry = {
  timestamp: string,
  level: logLevel,
  message: string,
  attributes: option<JSON.t>,
  @live
  resource: option<JSON.t>,
  @live
  consoleMethod: option<consoleMethod>,
}

type config = {
  bufferCapacity: int,
  stdoutPatterns: array<string>,
}

let defaultConfig: config = {
  bufferCapacity: 1024,
  stdoutPatterns: [
    "webpack",
    "turbopack",
    "Compiled",
    "Failed",
    "vite",
    "hmr",
    "error",
    "Error",
    "ERROR",
    "astro",
    "build",
  ],
}

type state = {
  buffer: ref<CircularBuffer.t<logEntry>>,
  config: config,
  insideConsoleHandler: ref<bool>,
}

let getGlobalInstanceOpt = (): option<state> => %raw(`globalThis.__FRONTMAN_CORE_INSTANCE__`)
let setGlobalInstance = (_state: state): unit =>
  %raw(`globalThis.__FRONTMAN_CORE_INSTANCE__ = _state`)

let getOrCreateInstance = (~config: config): state => {
  switch getGlobalInstanceOpt() {
  | Some(state) => state
  | None =>
    let state = {
      buffer: ref(CircularBuffer.make(~capacity=config.bufferCapacity)),
      config,
      insideConsoleHandler: ref(false),
    }
    setGlobalInstance(state)
    state
  }
}

let getInstance = (): state => {
  switch getGlobalInstanceOpt() {
  | Some(state) => state
  | None => getOrCreateInstance(~config=defaultConfig)
  }
}

let argsToString = (args: array<'a>): string => {
  args
  ->Array.map(arg => {
    switch arg->Type.typeof {
    | #string => arg->Obj.magic
    | #object =>
      if %raw(`arg instanceof Error`) {
        let error = arg->Obj.magic
        error["stack"]->Obj.magic->Option.getOr(error["message"]->Obj.magic)
      } else {
        arg->JSON.stringifyAny->Option.getOr("null")
      }
    | _ => arg->String.make
    }
  })
  ->Array.join(" ")
}

let stripAnsi = (str: string): string => {
  str->String.replaceRegExp(/\x1B(?:[@-Z\\-_]|\[[0-?]*[ -/]*[@-~])/g, "")
}

let addLog = (
  state: state,
  level: logLevel,
  message: string,
  ~attributes=?,
  ~consoleMethod=?,
): unit => {
  let cleanMessage = message->stripAnsi->String.trim

  switch cleanMessage != "" {
  | true =>
    let entry = {
      timestamp: Date.now()->Date.fromTime->Date.toISOString,
      level,
      message: cleanMessage,
      attributes,
      resource: None,
      consoleMethod,
    }
    state.buffer := state.buffer.contents->CircularBuffer.push(entry)
  | false => ()
  }
}

let detectLevel = (state: state, message: string): logLevel => {
  let matchesBuildPattern =
    state.config.stdoutPatterns->Array.some(pattern => message->String.includes(pattern))
  switch matchesBuildPattern {
  | true => Build
  | false => Console
  }
}

@@live
let handleConsoleLog = (state: state, args: array<'a>): unit => {
  let message = argsToString(args)
  addLog(state, detectLevel(state, message), message, ~consoleMethod=Log)
}

@@live
let handleConsoleWarn = (state: state, args: array<'a>): unit => {
  let message = argsToString(args)
  addLog(state, detectLevel(state, message), message, ~consoleMethod=Warn)
}

@@live
let handleConsoleError = (state: state, args: array<'a>): unit => {
  let message = argsToString(args)
  addLog(state, detectLevel(state, message), message, ~consoleMethod=ConsoleError)
}

@@live
let handleConsoleInfo = (state: state, args: array<'a>): unit => {
  let message = argsToString(args)
  addLog(state, detectLevel(state, message), message, ~consoleMethod=Info)
}

@@live
let handleConsoleDebug = (state: state, args: array<'a>): unit => {
  let message = argsToString(args)
  addLog(state, detectLevel(state, message), message, ~consoleMethod=Debug)
}

// Variadic interceptConsole implemented in raw JavaScript to handle variadic arguments
let interceptConsole: state => unit = %raw(`(function(state) {
  const originalLog = console.log.bind(console);
  const originalWarn = console.warn.bind(console);
  const originalError = console.error.bind(console);
  const originalInfo = console.info.bind(console);
  const originalDebug = console.debug.bind(console);

  console.log = (...args) => {
    state.insideConsoleHandler.contents = true;
    try { originalLog(...args); } finally { state.insideConsoleHandler.contents = false; }
    handleConsoleLog(state, args);
  };
  console.warn = (...args) => {
    state.insideConsoleHandler.contents = true;
    try { originalWarn(...args); } finally { state.insideConsoleHandler.contents = false; }
    handleConsoleWarn(state, args);
  };
  console.error = (...args) => {
    state.insideConsoleHandler.contents = true;
    try { originalError(...args); } finally { state.insideConsoleHandler.contents = false; }
    handleConsoleError(state, args);
  };
  console.info = (...args) => {
    state.insideConsoleHandler.contents = true;
    try { originalInfo(...args); } finally { state.insideConsoleHandler.contents = false; }
    handleConsoleInfo(state, args);
  };
  console.debug = (...args) => {
    state.insideConsoleHandler.contents = true;
    try { originalDebug(...args); } finally { state.insideConsoleHandler.contents = false; }
    handleConsoleDebug(state, args);
  };
})`)

@@live
let handleStdoutWrite = (state: state, message: string): unit => {
  switch state.insideConsoleHandler.contents {
  | true => ()
  | false =>
    let matchesPattern =
      state.config.stdoutPatterns->Array.some(pattern => message->String.includes(pattern))
    switch matchesPattern {
    | true => addLog(state, Build, message)
    | false => ()
    }
  }
}

let interceptStdout = (_state: state): unit => {
  %raw(`(function(_state) {
    const interceptStream = (stream) => {
      const originalWrite = stream.write.bind(stream);
      stream.write = (chunk, ...args) => {
        const message = typeof chunk === 'string' ? chunk : chunk.toString();
        handleStdoutWrite(_state, message);
        try {
          return originalWrite(chunk, ...args);
        } catch (e) {
          if (e && e.code === 'EPIPE') return false;
          throw e;
        }
      };
    };
    interceptStream(process.stdout);
    interceptStream(process.stderr);
  })(_state)`)
}

// Temporary inline bindings until workspace linking is fixed
type processError = {
  message: option<string>,
  stack: option<string>,
  name: string,
}

type rejectionReason
@get external getReasonMessage: rejectionReason => option<string> = "message"
@get external getReasonStack: rejectionReason => option<string> = "stack"
@scope("String") external stringFromReason: rejectionReason => string = "toString"

@val @scope("process")
external onProcessEvent: (string, 'a => unit) => unit = "on"

let interceptUncaughtErrors = (state: state): unit => {
  onProcessEvent("uncaughtException", (error: processError) => {
    try {
      let errorMessage = error.message->Option.getOr("Unknown error")
      let attributes =
        Dict.fromArray([
          ("stack", error.stack->Option.map(JSON.Encode.string)->Option.getOr(JSON.Encode.null)),
          ("name", error.name->JSON.Encode.string),
        ])->JSON.Encode.object
      addLog(state, Error, errorMessage, ~attributes)
    } catch {
    | _ => ()
    }
  })

  onProcessEvent("unhandledRejection", (reason: rejectionReason) => {
    try {
      let reasonMessage =
        reason
        ->getReasonMessage
        ->Option.getOr(reason->stringFromReason)
      let attributes = Dict.fromArray([
        (
          "stack",
          reason
          ->getReasonStack
          ->Option.map(JSON.Encode.string)
          ->Option.getOr(JSON.Encode.null),
        ),
      ])->JSON.Encode.object
      addLog(state, Error, reasonMessage, ~attributes)
    } catch {
    | _ => ()
    }
  })
}

let initialize = (~config: config=defaultConfig, ()): unit => {
  switch isBrowser() {
  | true => ()
  | false =>
    switch getPatchedFlag() {
    | Some(true) => ()
    | _ =>
      setPatchedFlag(true)
      let state = getOrCreateInstance(~config)
      interceptConsole(state)
      interceptStdout(state)
      interceptUncaughtErrors(state)
    }
  }
}

let regexCache = ref(None)

let getCompiledRegex = (pattern: string): RegExp.t => {
  switch regexCache.contents {
  | Some((cached, regex)) if cached === pattern => regex
  | _ =>
    let regex = RegExp.fromString(pattern, ~flags="i")
    regexCache := Some((pattern, regex))
    regex
  }
}

let getLogs = (
  ~pattern: option<string>=?,
  ~level: option<logLevel>=?,
  ~since: option<float>=?,
  ~tail: option<int>=?,
): array<logEntry> => {
  try {
    let state = getInstance()
    let allLogs = state.buffer.contents->CircularBuffer.toArray

    let logs = switch since {
    | Some(timestamp) =>
      allLogs->Array.filter(entry => entry.timestamp->Date.fromString->Date.getTime >= timestamp)
    | None => allLogs
    }

    let logs = switch level {
    | Some(filterLevel) => logs->Array.filter(entry => entry.level == filterLevel)
    | None => logs
    }

    let logs = switch pattern {
    | Some(p) =>
      let regex = getCompiledRegex(p)
      logs->Array.filter(entry => regex->RegExp.test(entry.message))
    | None => logs
    }

    switch tail {
    | Some(n) =>
      let len = logs->Array.length
      logs->Array.slice(~start=max(0, len - n), ~end=len)
    | None => logs
    }
  } catch {
  | _ => []
  }
}
