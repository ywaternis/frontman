@@jsxConfig({version: 4, mode: "automatic", module_: "BaseUi.BaseUiJsxDOM"})

@@directive("'use client'")

@@live

@module("tailwind-merge")
external cn: (string, option<string>) => string = "twMerge"

@module("tailwind-merge")
external cn3: (string, string, option<string>) => string = "twMerge"

module Align = {
  @unboxed
  type t =
    | @as("inline-start") InlineStart
    | @as("inline-end") InlineEnd
    | @as("block-start") BlockStart
    | @as("block-end") BlockEnd
}

module Size = {
  @unboxed
  type t =
    | @as("xs") Xs
    | @as("sm") Sm
    | @as("icon-xs") IconXs
    | @as("icon-sm") IconSm
}

module Variant = {
  @unboxed
  type t =
    | @as("ghost") Ghost
    | @as("default") Default
    | @as("secondary") Secondary
    | @as("outline") Outline
    | @as("destructive") Destructive
}

@get external mouseEventTarget: JsxEvent.Mouse.t => Dom.element = "target"
@get external mouseEventCurrentTarget: JsxEvent.Mouse.t => Dom.element = "currentTarget"
@get external parentElement: Dom.element => Nullable.t<Dom.element> = "parentElement"
@send external closest: (Dom.element, string) => Nullable.t<Dom.element> = "closest"
@send external querySelector: (Dom.element, string) => Nullable.t<Dom.element> = "querySelector"
@send external focusElement: Dom.element => unit = "focus"

@react.componentWithProps(BaseUi.Types.DomProps.t)
let make = (props: BaseUi.Types.DomProps.t) => {
  <div
    {...props}
    dataSlot="input-group"
    role="group"
    className={cn(
      "group/input-group relative flex h-8 w-full min-w-0 items-center rounded-lg border border-input transition-colors outline-none in-data-[slot=combobox-content]:focus-within:border-inherit in-data-[slot=combobox-content]:focus-within:ring-0 has-disabled:bg-input/50 has-disabled:opacity-50 has-[[data-slot=input-group-control]:focus-visible]:border-ring has-[[data-slot=input-group-control]:focus-visible]:ring-3 has-[[data-slot=input-group-control]:focus-visible]:ring-ring/50 has-[[data-slot][aria-invalid=true]]:border-destructive has-[[data-slot][aria-invalid=true]]:ring-3 has-[[data-slot][aria-invalid=true]]:ring-destructive/20 has-[>[data-align=block-end]]:h-auto has-[>[data-align=block-end]]:flex-col has-[>[data-align=block-start]]:h-auto has-[>[data-align=block-start]]:flex-col has-[>textarea]:h-auto dark:bg-input/30 dark:has-disabled:bg-input/80 dark:has-[[data-slot][aria-invalid=true]]:ring-destructive/40 has-[>[data-align=block-end]]:[&>input]:pt-3 has-[>[data-align=block-start]]:[&>input]:pb-3 has-[>[data-align=inline-end]]:[&>input]:pr-1.5 has-[>[data-align=inline-start]]:[&>input]:pl-1.5",
      props.className,
    )}
  />
}

module Addon = {
  let baseClass = "flex h-auto cursor-text items-center justify-center gap-2 py-1.5 text-sm font-medium text-muted-foreground select-none group-data-[disabled=true]/input-group:opacity-50 [&>kbd]:rounded-[calc(var(--radius)-5px)] [&>svg:not([class*='size-'])]:size-4"

  let alignClass = (~align=Align.InlineStart) =>
    switch align {
    | InlineStart => "order-first pl-2 has-[>button]:ml-[-0.3rem] has-[>kbd]:ml-[-0.15rem]"
    | InlineEnd => "order-last pr-2 has-[>button]:mr-[-0.3rem] has-[>kbd]:mr-[-0.15rem]"
    | BlockStart => "order-first w-full justify-start px-2.5 pt-2 group-has-[>input]/input-group:pt-2 [.border-b]:pb-2"
    | BlockEnd => "order-last w-full justify-start px-2.5 pb-2 group-has-[>input]/input-group:pb-2 [.border-t]:pt-2"
    }

  @react.component
  let make = (
    ~align=Align.InlineStart,
    ~className=?,
    ~children=?,
    ~id=?,
    ~style=?,
    ~onKeyDown=?,
  ) => {
    <div
      ?id
      ?children
      ?style
      onClick={event => {
        let target = event->mouseEventTarget
        switch target->closest("button") {
        | Value(_) => ()
        | Null | Undefined =>
          event
          ->mouseEventCurrentTarget
          ->parentElement
          ->Nullable.flatMap(parent => parent->querySelector("input"))
          ->Nullable.forEach(focusElement)
        }
      }}
      ?onKeyDown
      dataSlot="input-group-addon"
      dataAlign={(align :> string)}
      role="group"
      className={cn3(baseClass, alignClass(~align), className)}
    />
  }
}

module Button = {
  type type_ =
    | @as("button") Button
    | @as("submit") Submit
    | @as("reset") Reset

  let sizeClass = (~size: Size.t) =>
    switch size {
    | Xs => "h-6 gap-1 rounded-[calc(var(--radius)-3px)] px-1.5 [&>svg:not([class*='size-'])]:size-3.5"
    | Sm => ""
    | IconXs => "size-6 rounded-[calc(var(--radius)-3px)] p-0 has-[>svg]:p-0"
    | IconSm => "size-8 p-0 has-[>svg]:p-0"
    }

  let baseClass = "flex items-center gap-2 text-sm shadow-none"

  let buttonVariant = (~variant: Variant.t): Client__UI__Button.Variant.t =>
    switch variant {
    | Ghost => Ghost
    | Default => Default
    | Secondary => Secondary
    | Outline => Outline
    | Destructive => Destructive
    }

  @react.component
  let make = (
    ~className=?,
    ~children=?,
    ~type_=Button,
    ~dataSlot="button",
    ~size=Size.Xs,
    ~variant=Variant.Ghost,
    ~id=?,
    ~style=?,
    ~onClick=?,
    ~onKeyDown=?,
    ~disabled=?,
    ~dataActive=?,
    ~ariaPressed=?,
    ~ariaLabel=?,
    ~render=?,
    ~nativeButton=?,
  ) => {
    <Client__UI__Button
      ?id
      ?children
      ?style
      ?onClick
      ?onKeyDown
      ?disabled
      ?dataActive
      ?ariaPressed
      ?ariaLabel
      ?render
      ?nativeButton
      type_={(type_ :> string)}
      variant={buttonVariant(~variant)}
      dataSlot
      dataSize={(size :> string)}
      className={cn3(baseClass, sizeClass(~size), className)}
    />
  }
}

module Text = {
  @react.component
  let make = (~className=?, ~children=?, ~id=?, ~style=?, ~onClick=?, ~onKeyDown=?) =>
    <span
      ?id
      ?children
      ?style
      ?onClick
      ?onKeyDown
      className={cn(
        "text-muted-foreground flex items-center gap-2 text-sm [&_svg]:pointer-events-none [&_svg:not([class*='size-'])]:size-4",
        className,
      )}
    />
}

module Input = {
  @react.component
  let make = (
    ~className=?,
    ~children=?,
    ~id=?,
    ~style=?,
    ~onClick=?,
    ~onKeyDown=?,
    ~type_=?,
    ~placeholder=?,
    ~value=?,
    ~defaultValue=?,
    ~onValueChange=?,
    ~disabled=?,
    ~name=?,
  ) =>
    <Client__UI__Input
      ?id
      ?style
      ?onClick
      ?onKeyDown
      ?children
      ?type_
      ?placeholder
      ?value
      ?defaultValue
      ?onValueChange
      ?disabled
      ?name
      dataSlot="input-group-control"
      className={cn(
        "flex-1 rounded-none border-0 bg-transparent shadow-none ring-0 focus-visible:ring-0 disabled:bg-transparent aria-invalid:ring-0 dark:bg-transparent dark:disabled:bg-transparent",
        className,
      )}
    />
}

module Textarea = {
  @react.component
  let make = (
    ~className=?,
    ~children=?,
    ~id=?,
    ~style=?,
    ~name=?,
    ~placeholder=?,
    ~value=?,
    ~defaultValue=?,
    ~disabled=?,
    ~readOnly=?,
    ~required=?,
    ~maxLength=?,
    ~spellCheck=?,
    ~onClick=?,
    ~onKeyDown=?,
  ) =>
    <textarea
      ?id
      ?children
      ?style
      ?name
      ?placeholder
      ?value
      ?defaultValue
      ?disabled
      ?readOnly
      ?required
      ?maxLength
      ?spellCheck
      ?onClick
      ?onKeyDown
      dataSlot="input-group-control"
      className={cn(
        "flex-1 resize-none rounded-none border-0 bg-transparent py-2 shadow-none ring-0 focus-visible:ring-0 disabled:bg-transparent aria-invalid:ring-0 dark:bg-transparent dark:disabled:bg-transparent",
        className,
      )}
    />
}
